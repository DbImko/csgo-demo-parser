
plugins {
    id "java"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.7.3"
}
apply plugin: com.github.dbimko.gradle.VersionPlugin
apply plugin: com.github.dbimko.gradle.JavaPlugin

repositories {
    jcenter()
    maven {url = 'https://dl.bintray.com/dbimko/maven'}
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    compileOnly("org.projectlombok:lombok:${lombokVersion}")
    compile("com.github.dbimko:csgo-demo-protobuf-messages:1.0.0.7")

    compileOnly("org.slf4j:slf4j-api:1.7.25")
    compileOnly("ch.qos.logback:logback-classic:1.1.3")

    compile("com.google.guava:guava:18.0")
    compile("com.google.protobuf:protobuf-java:3.3.0")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from(sourceSets.main.allSource)
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from(javadoc.destinationDir)
}

artifacts {
    archives(sourcesJar, javadocJar)
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
            artifact(sourcesJar)
            artifact(javadocJar)
        }
    }
}

bintray {
    user = System.getenv('bintray_user')
    key = System.getenv('bintray_api_key')
    publish = true
    pkg {
        publications = ['maven']
        repo = 'maven'
        userOrg = 'dbimko'
        name = project.name
        desc = project.description
        websiteUrl = 'https://github.com/DbImko/csgo-demo-parser'
        vcsUrl = 'https://github.com/DbImko/csgo-demo-parser'
        licenses = ['Apache-2.0']
        publicDownloadNumbers = true
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}

